plugins {
    id 'com.gradleup.shadow'
    id 'me.modmuss50.mod-publish-plugin'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"

    // Architectury API. This is optional, and you can comment it out if you don't need it.
    modImplementation "dev.architectury:architectury-neoforge:$rootProject.architectury_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')
    shadowBundle "blue.endless:jankson:$rootProject.jankson_version"
    runtimeOnly "blue.endless:jankson:$rootProject.jankson_version"
    shadowBundle "com.github.ben-manes.caffeine:caffeine:$rootProject.caffeine_version"
    runtimeOnly "com.github.ben-manes.caffeine:caffeine:$rootProject.caffeine_version"

    // Config dependency (optional at runtime)
    modImplementation "dev.isxander:yet-another-config-lib:$rootProject.yacl_version_neoforge"
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/neoforge.mods.toml') {
        expand version: inputs.properties.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}

publishMods {
    file = remapJar.archiveFile
    changelog = {
        def cl = findProperty('changelog') ?: env.fetchOrNull('CHANGELOG')
        if (!cl) throw new GradleException('Provide a changelog via -Pchangelog="..." or the CHANGELOG environment variable')
        cl
    }()
    type = STABLE
    modLoaders.add("neoforge")

    modrinth {
        accessToken = env.fetchOrNull("MODRINTH_TOKEN")
        projectId = rootProject.modrinth_project_id
        projectDescription = rootProject.file("description.md").text
        minecraftVersions.add(rootProject.minecraft_version)
        requires("architectury-api")
        optional("yacl")
    }

    curseforge {
        accessToken = env.fetchOrNull("CURSEFORGE_TOKEN")
        projectId = rootProject.curseforge_project_id
        minecraftVersions.add(rootProject.minecraft_version)
        requires("architectury-api")
        optional("yacl")
    }

    github {
        accessToken = env.fetchOrNull("GITHUB_TOKEN")
        parent rootProject.tasks.named("publishGithub")
    }
}
