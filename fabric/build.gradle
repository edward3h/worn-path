plugins {
    id 'com.gradleup.shadow'
    id 'me.modmuss50.mod-publish-plugin'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"

    // Architectury API. This is optional, and you can comment it out if you don't need it.
    modImplementation "dev.architectury:architectury-fabric:$rootProject.architectury_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')
    shadowBundle "blue.endless:jankson:$rootProject.jankson_version"
    runtimeOnly "blue.endless:jankson:$rootProject.jankson_version"
    shadowBundle "com.github.ben-manes.caffeine:caffeine:$rootProject.caffeine_version"
    runtimeOnly "com.github.ben-manes.caffeine:caffeine:$rootProject.caffeine_version"

    // Config dependencies (optional at runtime)
    modImplementation "dev.isxander:yet-another-config-lib:$rootProject.yacl_version"
    modImplementation "com.terraformersmc:modmenu:$rootProject.modmenu_version"
}

processResources {
    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand version: inputs.properties.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}

publishMods {
    file = remapJar.archiveFile
    changelog = {
        def cl = findProperty('changelog') ?: env.fetchOrNull('CHANGELOG')
        if (!cl && gradle.startParameter.taskNames.any { it.toLowerCase().contains('publish') }) {
            throw new GradleException('Provide a changelog via -Pchangelog="..." or the CHANGELOG environment variable')
        }
        cl ?: ''
    }()
    type = STABLE
    modLoaders.add("fabric")

    modrinth {
        accessToken = env.fetchOrNull("MODRINTH_TOKEN")
        projectId = rootProject.modrinth_project_id
        projectDescription = rootProject.file("description.md").text
        minecraftVersions.add(rootProject.minecraft_version)
        requires("architectury-api")
        requires("fabric-api")
        optional("yacl")
        optional("modmenu")
    }

    curseforge {
        accessToken = env.fetchOrNull("CURSEFORGE_TOKEN")
        projectId = rootProject.curseforge_project_id
        minecraftVersions.add(rootProject.minecraft_version)
        requires("architectury-api")
        requires("fabric-api")
        optional("yacl")
        optional("modmenu")
    }

    github {
        accessToken = env.fetchOrNull("GITHUB_TOKEN")
        parent rootProject.tasks.named("publishGithub")
    }
}
